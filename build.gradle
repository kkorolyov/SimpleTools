buildscript {
	repositories {
		maven {
			url 'https://dl.bintray.com/kkorolyov/groovy'  // FullDoc
		}
	}
	dependencies {
		classpath 'dev.kkorolyov:full-doc:1.+'
	}
}
plugins {
	id 'java-library'
	id 'groovy'
	id 'maven-publish'
	id 'com.jfrog.bintray' version '1.8.0'
	id 'nebula.dependency-lock' version '5.0.6'
}
apply plugin: 'dev.kkorolyov.full-doc'

fulldoc {
	src 'docs'
	out "$buildDir/docs"
}

subprojects {
	apply plugin: 'java-library'
	apply plugin: 'groovy'
	apply plugin: 'maven-publish'
	apply plugin: 'com.jfrog.bintray'
	apply plugin: 'nebula.dependency-lock'

	group = 'dev.kkorolyov'
	description = "Collection of utility libraries with simple APIs"

	repositories {
		jcenter()
	}

	dependencies {
		testImplementation(
				project(':simple-props'),
				project(':simple-specs'),
				'org.spockframework:spock-core:1.1-groovy-2.4',
				'net.bytebuddy:byte-buddy:1.+'
		)
	}

	task sourcesJar(type: Jar) {
		classifier = 'sources'
		from sourceSets.main.allJava
	}
	task javadocJar(type: Jar) {
		classifier = 'javadoc'
		from javadoc.destinationDir
	}
	publishing {
		publications {
			Pub(MavenPublication) {
				from components.java
				artifact sourcesJar
				artifact javadocJar
			}
		}
	}

	afterEvaluate {
		bintray {
			user = System.getenv('BINTRAY_USER')
			key = System.getenv('BINTRAY_KEY')

			publications = ['Pub']
			publish = true
			override = true

			pkg {
				repo = 'java'
				name = project.name
				licenses = ['BSD 3-Clause']
				vcsUrl = 'https://github.com/kkorolyov/SimpleTools.git'
				version {
					name = project.version
				}
			}
		}
	}
}

project(':simple-logs') {
	description = "Lightweight Java logging library with minimal configuration"
	version = '3.1'

	dependencies {
		compileOnly project(':simple-props')
	}
}

project(':simple-props') {
	description = "Lightweight Java properties library"
	version = '4.2'

	dependencies {
		testImplementation 'junit:junit:4.+'
	}
}

project(':simple-files') {
	description = "Snappy file interaction library"
	version = '1.1.1'
}

project(':simple-opts') {
	description = "Lightweight Java argument parsing library"
	version = '1.1'

	dependencies {
		testImplementation 'junit:junit:4.+'
	}
}

project(':simple-funcs') {
	description = "Java functional programming extensions"
	version = '1.3'
}

project(':simple-structs') {
	description = "Java library of various data structures and algorithms"
	version = '1.0'

	dependencies {
		implementation project(':simple-funcs')
	}
}

project(':simple-specs') {
	description = "Groovy test/specification utilities"
	version = '1.0.1'

	dependencies {
		implementation 'org.codehaus.groovy:groovy:2.4.+'
	}
}
